buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.9.+'
    }
}
apply plugin: 'android'

repositories {
    mavenCentral()
    maven {
        url "http://www.bugsense.com/gradle/"
    }
}

android {
    compileSdkVersion 19
    buildToolsVersion "19.1.0"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 19
        versionCode 3
        versionName "1.0.2"
    }

    final Console console = System.console();

    if (console != null) {

        signingConfigs {
            release {
                storeFile file(console.readLine("\nEnter keystore path: "))
                storePassword console.readLine("Enter keystore password: ")
                keyAlias console.readLine("Enter alias key: ")
                keyPassword console.readLine("Enter key password: ")
            }
        }

    } else {
        signingConfigs {
            release {
            }
        }
    }

    buildTypes {
        release {
            runProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:support-v4:19.0.1'
    compile 'com.android.support:appcompat-v7:19.0.1'
    compile "com.netflix.rxjava:rxjava-core:0.19.+"
    compile "com.netflix.rxjava:rxjava-async-util:0.19.+"
    compile "com.netflix.rxjava:rxjava-android:0.19.+"
    compile "com.bugsense.trace:bugsense:3.5"
    compile 'org.jsoup:jsoup:1.7.3'
    compile project(':directoryChooser')

}
